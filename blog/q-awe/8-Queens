<code><pre>
<b> Idea:</b>
<b>       2                          6            3</b>
<b>   . . . . . . . .    . . . . . . . .    . . . . . . . .</b>
<b>   . . . . . . . .    . . . . . . . .    . . . . . . . .</b>
<b>   . . . . . . . .    . . . . . . . .    . . . . . . . .</b>
<b>   . . . . . . . .    . . . . . . . .    . . . . . . . .</b>
<b>   . . . . . . . .    . . . . . . . .    . . . . . . . .</b>
<b>   . . . . . . . .    . . . . . . . .  2 \ . | Q / \ | /</b>
<b>   . . . . . . . .  1 . \ | / . . Q .    . \ | / . . Q .</b>
<b> 0 . . Q . . . . .    . . Q . . . . .    . . Q . . . . .</b>

<b>   2 -> 2,2-(3),2+(3)</b>
<b>   6 -> 6,6-(2),6+(2)</b>
<b>   3 -> 3,3-(1),3+(1)</b>

<b>   2 6 3 => (2 6 3;</b>
<b>             2 6 3 + 3 2 1;</b>
<b>             2 6 3 - 3 2 1)</b>

<b>   {x,(x-f),x+f:reverse 1+til count x}</b>

q)/q solution
q)f:{raze {(x,) each (til 8) except {x,(x-f),x+f:reverse 1+til count x} x} each x}
q)\ts:10 7 f/til 8
<i>248 100128</i>
q)q8:7 f/til 8
q)count q8
<i>92</i>
q)first q8
<i>0 4 7 5 2 6 1 3</i>
q)-1 (ssr[;enlist"?";{" ",x}] ".Q" (til 8)=) each q8 0;
<b> Q . . . . . . .</b>
<b> . . . . Q . . .</b>
<b> . . . . . . . Q</b>
<b> . . . . . Q . .</b>
<b> . . Q . . . . .</b>
<b> . . . . . . Q .</b>
<b> . Q . . . . . .</b>
<b> . . . Q . . . .</b>

k)/k solution
k)f:{,/{(x,)'(!8)@&~(!8)in{x,(x-f),x+f:|1+!#x}x}'x}
k)\ts:10 7 f/!8
<i>144 100112</i>
k)#7 f/!8
<i>92</i>
k)*7 f/!8
<i>0 4 7 5 2 6 1 3</i>

k)/k solution N
k)#{(x-1){,/{(x,)'(!y)@&~(!y)in{x,(x-f),x+f:|1+!#x}x}[;y]'x}[;x]/!x}8
<i>92</i>
</pre></code>
